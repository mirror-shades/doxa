module std from "std"

enum Target {
    x86_windows,
    x86_macos,
    x86_linux,
    aarch_macos,
    aarch_linux,
    not_found,
}

function getTargetString(target :: Target) returns string {
    return match target {
        .x86_windows then "x86_64-windows",
        .x86_macos then "x86_64-macos",
        .x86_linux then "x86_64-linux",
        .aarch_macos then "aarch64-macos",
        .aarch_linux then "aarch64-linux",
        .not_found then "not-found",
    }
}

entry function main() {
    const os is @os()
    const arch is @arch()
    var target :: Target is .not_found

    if os == "windows" then {
        if arch == "x86_64" then target is .x86_windows
    }
    if os == "mac" then {
        if arch == "x86_64" then target is .x86_macos
        if arch == "aarch64" then target is .aarch_macos
    }
    if os == "linux" then {
        if arch == "x86_64" then target is .x86_linux
        if arch == "aarch64" then target is .aarch_linux
    }

    if target == .not_found {
        @print("build not supported for this target system")
        return
    }

    const target_string is "zig-" + getTargetString(target) + "-0.15.2"
    const target_zip_string is target_string + ".zip"
    const target_url_string is "https://ziglang.org/download/0.15.2/" + target_zip_string

    std.process.exec("zig build")
    std.file.makeDir("doxa-build")
    std.file.copyDir("zig-out/bin", "doxa-build/bin")
    std.file.copyDir("std", "doxa-build/lib/std")
    std.http.downloadUrl(target_url_string, target_zip_string)
    std.file.unzipFile(target_zip_string, ".")
    std.file.copyDir(target_string, "doxa-build/lib/zig")
    std.file.delete(target_string, true)
    std.file.delete(target_zip_string, true)
}