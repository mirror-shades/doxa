module std from "std"

enum Target {
    x86_windows,
    x86_macos,
    x86_linux,
    aarch_macos,
    aarch_linux,
    not_found,
}

function getTargetString(target :: Target) returns string {
    return match target {
        .x86_windows then "x86_64-windows",
        .x86_macos then "x86_64-macos",
        .x86_linux then "x86_64-linux",
        .aarch_macos then "aarch64-macos",
        .aarch_linux then "aarch64-linux",
        .not_found then "not-found",
    }
}

function setTarget(^target :: Target, os :: string, arch :: string) {
    if os == "windows" then {
        if arch == "x86_64" then target is .x86_windows
    }
    if os == "mac" then {
        if arch == "x86_64" then target is .x86_macos
        if arch == "aarch64" then target is .aarch_macos
    }
    if os == "linux" then {
        if arch == "x86_64" then target is .x86_linux
        if arch == "aarch64" then target is .aarch_linux
    }
}

function build(os :: string, arch :: string) {
    var target :: Target is .not_found
    setTarget(^target, os, arch)

    if target == .not_found {
        @print("build not supported for this target system")
        return
    }

    const target_string is "zig-" + getTargetString(target) + "-0.15.2"
    const target_zip_string is target_string + ".zip"
    const target_url_string is "https://ziglang.org/download/0.15.2/" + target_zip_string

    @print("building for {arch}-{os}\n")

    @print("compilng Doxa\n")
    std.process.exec("zig build\n")

    @print("making build dir\n")
    std.file.makeDir("doxa-build")
    
    @print("copying doxa bin\n")
    std.file.copyDir("zig-out/bin", "doxa-build/bin")
   
    @print("copying doxa std\n")
    std.file.copyDir("std", "doxa-build/lib/std")
    
    @print("downloading zig dependancy\n")
    std.http.downloadUrl(target_url_string, target_zip_string)
    
    @print("unzipping\n")
    std.file.unzipFile(target_zip_string, ".")

    @print("copying zig\n")
    std.file.copyDir(target_string, "doxa-build/lib/zig")
    
    @print("cleaning up\n")
    std.file.delete(target_string, true)
    std.file.delete(target_zip_string, true)
}

entry function main() {
    const os is @os()
    const arch is @arch()
    build(os, arch)
}