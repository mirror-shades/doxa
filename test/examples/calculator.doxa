enum TokenType {
    NUMBER,
    OPERATOR,
    LPAREN,
    RPAREN,
}

struct Token {
    token_type :: TokenType,
    value :: string | int,
}

entry fn main() {
    // all compiler level methods are distinguished by @
    const input is @input()

    const splitInput is split(input)

    splitInput?

    const possibleTokens is tokenize(splitInput)

    // union needs to be narrowed to catch our error returns
    possibleTokens as Token[] then {
        execute(possibleTokens)
    } else {
        "tokens failed to parse"?
        return
    }

    return
}

fn split(input :: string) -> string[] {
    var result :: string[]
    var current is ""

    const lin is @length(input)

    // `each` loops iterate through each element
    // they can work on strings as well as arrays
    // `at` is optional syntax for a loop index
    each c at i in input {
        // breaks number if operator is found
        if @length(current) > 0 and (c equals " " or c equals "+" or c equals "-" or c equals "*" or c equals "/") then {
            @push(result, current)
            current is ""
        }

        // make sure - isn't a negative sign
        if c equals "-" and input[i+1] equals " " then {
            @push(result, c)
        } 

        else if c equals "+" or c equals "*" or c equals "/" then {
            @push(result, c)
        } else if c != " " {
            current is current + c
        }

    }
    @push(result, current)
    return result
}

fn tokenize(input :: string[]) -> Token[] | nothing {
    var tokens :: Token[]

    each token in input {
        if token equals "+" or token equals "-" or token equals "*" or token equals "/" then {
            @push(tokens, Token {
                token_type is .OPERATOR,
                value is token,
            })
        } 
        else if token equals "(" then {
            @push(tokens, Token {
                token_type is .LPAREN,
                value is token,
            })
        } 
        else if token equals ")" then {
            @push(tokens, Token {
                token_type is .RPAREN,
                value is token,
            })
        } else {
            // @int returns a union type, so we need to narrow it down to an int
            // at this point we are not sure is the token is a valid int
            var possibleNumber is @int(token)
            // the `as` keyword checks to see if the union is an int
            possibleNumber as int then {
                // if the token is a valid int, it is narrowed in the then block
                @push(tokens, Token { 
                    token_type is .NUMBER,
                    value is possibleNumber,
                })
            } else {
                // if @int cannot be narrowed an error was returned
                // we can print debug info and exit the function
                token?;
                "invalid token, skipping"?;
                // return nothing
                return
            }
        }
    }
    return tokens
}

fn execute(tokens :: Token[]) {
	var total :: int is 0
	const first is tokens[0].value
	first as int then {
		total is first
	} else {
		tokens[0].token_type?;
		tokens[0].value?;
		"first value must be a number"?;
		return
	}
	each token at i in tokens {
		if i != 0 then {
			token.token_type?;
			if token.token_type equals TokenType.OPERATOR then {
				// narrow operator value to string and process in success branch
				const op is token.value as string else {
					token.value?;
					"value must be an operator"?;
					return
				}

				const next is tokens[i + 1]
				const rightHand is next.value as int else {
					next.value?;
					"value must be a number"?;
					return
				}

				if op equals "+" then { total += rightHand }
				else if op equals "-" then { total -= rightHand }
				else if op equals "*" then { total *= rightHand }
				else if op equals "/" then { total /= rightHand }
				else { op?; "invalid operator"?; return }
			}
		}
	}
	total?
	return
}