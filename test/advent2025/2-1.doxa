entry function main() {
    const input is @read("./advent2.txt")
    const split_input is split(input, ",")
    var invalid_sum :: int
    
    each pair in split_input {
        const split_pair is split(pair, "-")
        var current is @int(split_pair[0])
        var limit is @int(split_pair[1])
        @print("checking {current}-{limit} (total {limit - current + 1})\n")
        while current <= limit do current += 1 {
            if isInvalid(current) then {
                invalid_sum += current
            } 
        }
    }
    @print("Invalid sum: {invalid_sum}\n")
}

function split(input :: string, separator :: string) returns string[] {
    if @length(input) == 0 then return []
    var result :: string[]
    var current is ""
    each c in input {
        if c == separator then {
            @push(result, current)
            current is ""
        } else {
            current += c
        }
    }
    @push(result, current)
    return result
}

function isInvalid(number :: int) returns tetra {
    # invalid numbers are always even
    var s_number is @string(number)
    if @length(s_number) % 2 != 0 then return false
    var half_length is @int(@length(s_number) / 2)
    for x while x < half_length do x += 1 {
        if s_number[x] != s_number[half_length + x] then return false
    }
    return true
}