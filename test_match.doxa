enum TokenType {
    NUMBER,
    OPERATOR,
    PARENTHESIS,
}

struct Token {
    token_type :: TokenType,
    value :: string | int,
}

entry fn main() {
    // all compiler level methods are distinguished by @
    const input is @input()

    const splitInput is split(input)

    const tokens is tokenize(splitInput)

    execute(tokens)

    return
}

fn split(input :: string) -> string[] {
    var result :: string[]
    var current is ""

    const lin is @length(input)

    // each loops iterate through each element
    // they can work on strings as well as arrays
    each c in input {
        if @length(current) > 0 and (c equals " " or c equals "+" or c equals "-" or c equals "*" or c equals "/") then {
            @push(result, current)
            current is ""
        }

        if c equals " " then {
            // Skip spaces
        }
        else if c equals "+" or c equals "-" or c equals "*" or c equals "/" then {
            @push(result, c)
        } else {
            current is current + c
        }

    }
    @push(result, current)
    return result
}

fn tokenize(input :: string[]) -> Token[] {
    var tokens :: Token[]

    each token in input {
        if token equals "+" or token equals "-" or token equals "*" or token equals "/" then {
            @push(tokens, Token {
                token_type is .OPERATOR,
                value is token,
            })
        } 
        else if token equals "(" or token equals ")" then {
            @push(tokens, Token {
                token_type is .PARENTHESIS,
                value is token,
            })
        } else {
            // @int returns a union type, so we need to narrow it down to an int
            // at this point we are not sure is the token is a valid int
            var possibleNumber is @int(token)
            // the `as` keyword checks to see if the union is an int
            possibleNumber as int then {
                // if the token is a valid int, it is narrowed in the then block
                @push(tokens, Token { 
                    token_type is .NUMBER,
                    value is possibleNumber,
                })
            } else {
                // if @int cannot be narrowed an error was returned
                // we can print the unnarrowed value, then skip this token
                token?
                "invalid token, skipping"?
            }
        }
    }
    return tokens
}

fn execute(tokens :: Token[]) {
	var total :: int is 0
	const first is tokens[0].value
	first as int then {
		total is first
	} else {
		tokens[0].token_type?
		tokens[0].value?
		"first value must be a number"?
		return
	}
	each token at i in tokens {
		if i != 0 then {
			if token.token_type equals TokenType.OPERATOR then {
                // we can use `as` to both narrow and handle errors
                const op is token.value as string else {
                    token.value?
                    "value must be an operator"?
                    return
                }

				const rightHand is tokens[i + 1].value as int else {
					token.value?
					"value must be a number"?
					return
                }

                if op equals "+" then total += rightHand
                else if op equals "-" then total -= rightHand
                else if op equals "*" then total *= rightHand
                else if op equals "/" then total /= rightHand
                else {
                    op?
                    "invlaid operator"?
                    return
                }
			}
		}
	}
	total?
	return
}